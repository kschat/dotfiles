#!/usr/bin/env zsh

set -e
trap 'report_error' ERR

# needed to send notifications as root
# TODO don't hardcode
# export DBUS_SESSION_BUS_ADDRESS=unix:path=/run/user/1000/bus
# export XDG_RUNTIME_DIR=/run/user/1000

filter_file=/tmp/backup-filters

cat << EOM >"$filter_file"
- /dev/*
- /proc/*
- /sys/*
- /tmp/*
- /run/*
- /mnt/*
- /var/run/*
- /var/cache/*
- /var/tmp/*
- /var/lock/*
- /media/*
- /home/**/.cache/*
- /home/**/.Cache/*
- /home/**/cache/*
- /home/**/Cache/*
- /home/kyle/.config/**/GPUCache/*
- /home/kyle/.config/**/IndexedDB/*
- /home/kyle/.config/google-chrome/*
- /home/kyle/.config/google-chrome-beta/*
- /home/kyle/.config/chromium/*
- /home/kyle/Workspace/**/node_modules/*
- /**/logs/*
- /**/log/*
+ /usr/local/
- /usr/*
EOM

log_and_quit() {
  >&2 echo $1
  exit 1
}

report_error() {
  /home/kyle/.bin/notify-all -a 'backup' 'Error occured while backing up system'
}

extract_ssh_creds() {
  local dir=$1
  echo "$dir" | awk -F':' '{print $1}'
}

extract_ssh_path() {
  local dir=$1
  echo "$dir" | awk -F':' '{print $2}'
}

sync_files() {
  local source_dir=$1
  local destination_dir=$2
  local link_dir=$3

  rsync \
    --rsh ssh \
    --archive \
    --xattrs \
    --partial \
    --delete \
    --delay-updates \
    --sparse \
    --progress \
    --human-readable \
    --itemize-changes \
    --link-dest="$link_dir" \
    --filter="merge $filter_file" \
    "$source_dir" \
    "$destination_dir"
}

backup() {
  [[ -z $1 ]] || [[ -z $2 ]] && log_and_quit 'Missing required input'

  local now=$(date '+%Y-%m-%d_%T')
  local source_dir=$1
  local snapshot_dir=$(extract_ssh_path "$2")
  local ssh_creds=$(extract_ssh_creds "$2")
  local latest_dir="$snapshot_dir/latest"
  local current_dir="$snapshot_dir/$now"

  ssh "$ssh_creds" "mkdir -p '$snapshot_dir'"

  sync_files "$source_dir" "$ssh_creds:$current_dir" "$latest_dir" || {
    local error_code=$?
    [[ $error_code -ne 23 ]] && return "$error_code"
  }

  ssh "$ssh_creds" "rm -f '$latest_dir'"
  ssh "$ssh_creds" "ln -sF '$current_dir' '$latest_dir'"
}

ionice -c 3 -p $$
renice +10 -p $$

notify-send -a 'backup' 'System backup running...'

backup / kyle@truenas:/mnt/nas/desktop-backup

notify-send -a 'backup' 'System backup complete'

