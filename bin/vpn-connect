#!/usr/bin/env zsh

set -e

export SUDO_ASKPASS=/home/kyle/Workspace/dotfiles/bin/prompt-dialog

pid_file=/tmp/vpn-connect-pid
status_file=/tmp/vpn-connect-status
tunnel=false

typeset -A state; state=(
  disconnected DISCONNECTED
  connecting CONNECTING
  connected CONNECTED
)

is_running() {
  pgrep -F "$pid_file" >/dev/null 2>&1
  return $?
}

get_status() {
  cat "$status_file" 2>/dev/null || echo "${state[disconnected]}"
}

get_status_file() {
  if ! is_running; then
    echo "${state[disconnected]}" >! "$status_file"
  fi

  echo "$status_file"
}

shutdown() {
  : >! "$pid_file"
  [ "$(get_status)" = "${state[connected]}" ] && notify-all -a 'vpn' 'Disconnected'
  echo "${state[disconnected]}" >! "$status_file"
  kill 0
}

report_error() {
  notify-all -a 'vpn' 'Error occured connecting to VPN'
}

while test $# -gt 0; do
  case "$1" in
    -s|--status) get_status; exit 0;;
    -f|--status-file) get_status_file; exit 0;;
    -q|--quiet) exec 1> /tmp/vpn-connect.log; exec 2>&1; shift;;
    -t|--tunnel) tunnel=true; shift;;
    *) echo "Unknown parameter $1"; exit 1;;
  esac
done

if is_running; then
  notify-all -a 'vpn' 'VPN client already running'
  exit 1
fi

echo $$ > "$pid_file"
trap 'trap - TERM; shutdown' INT TERM QUIT EXIT
trap 'report_error' ERR

# ask for sudo permissions upfront
sudo --validate --askpass

# keep permissions for the duration of the script
while true; do sudo -n true; sleep 60; kill -0 "$$" || exit; done 2>/dev/null &

pin=$(prompt-dialog 'token')
password=$(bw get password 480c961c-d8f5-474c-ad77-aa4401890b96)

echo "${state[connecting]}" > "$status_file"
notify-all -a 'vpn' 'Connecting to VPN...'

if [ "$tunnel" = true ]; then
  echo "$password\n$pin" | sudo openconnect \
    --user kschat2 \
    --non-inter \
    --passwd-on-stdin \
    --background \
    --script vpn-wrapper-script \
    https://sslvpngwgec.wal-mart.com/Walmart_Associate_MAC_Yubi
else
  echo "$password\n$pin" | sudo openconnect \
    --user kschat2 \
    --non-inter \
    --passwd-on-stdin \
    --background \
    https://sslvpngwgec.wal-mart.com/Walmart_Associate_MAC_Yubi
fi

echo "${state[connected]}" > "$status_file"
notify-all -a 'vpn' 'Connected to VPN'

wait

