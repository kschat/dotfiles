# vi: filetype=zsh

# Load dependencies.
pmodload 'helper'

function prompt_kyle_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # get Git repository information
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_kyle_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_kyle_precmd

  hname=" at %F{blue}%m%f"

  # required to use editor mode
  zstyle ':prezto:module:prompt' managed 'yes'

  # Note: the "current" node decides the fg color (the color of the round border), but does not set the bg color (the
  # color of the "next" node). The next node sets its background color and then displays the round border. This is so
  # dynamic ordering of nodes renders the correct background color (e.g. when you cd from a git repo to a non-git repo).

  # editor mode info
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{cyan}%f%F{black}%K{cyan}%B I %b%k%f%F{cyan}'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{blue}%f%F{black}%K{blue}%B N %b%k%f%F{blue}'

  # git info
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:clean' format ' ✔'
  zstyle ':prezto:module:git:info:dirty' format ' '
  zstyle ':prezto:module:git:info:stashed' format ' ✚'
  zstyle ':prezto:module:git:info:untracked' format ' ●'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%K{black}%k%f%F{white}%K{black} %b%C%D%S%u %k%f%F{black}' \
    'rprompt' ''

  PROMPT=$'\n${(e)editor_info[keymap]}'
  PROMPT+=$'${(e)git_info[prompt]}'
  PROMPT+=$'%K{white}%k%f%F{black}%K{white} %~ %k%f%F{white}%f'
  PROMPT+=$'\n —— '
}

prompt_kyle_setup "$@"
